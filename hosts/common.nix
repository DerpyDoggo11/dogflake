{ pkgs, lib, config, ... }: {
  users.users.dog = { # Default user
    isNormalUser = true;
    extraGroups = [ "wheel" "audio" "video" "dialout" ];
    # Password comes from /persist/passwords/dog - see modules/tmpfs-root.nix
  };

  boot = {
    loader = {
      systemd-boot = {
        enable = lib.mkDefault true;
        configurationLimit = 2;
        editor = false;
      };
      efi.canTouchEfiVariables = true;
      timeout = lib.mkForce 0; # Hold down space on boot to access
    };
    tmp.useTmpfs = true;
    kernelPackages = lib.mkDefault pkgs.linuxPackages_latest;
    kernelParams = [ "nowatchdog" "nmi_watchdog=0" ];
    blacklistedKernelModules = [ "sp5100_tco" ]; # speeds up shutdown on amd stop since it doesnt wait for watchdog
    kernelModules = [ "tcp_bbr" ];

    # faster networking
    kernel.sysctl = {
      "net.ipv4.tcp_congestion_control" = "bbr";
      "net.core.default_qdisc" = "fq"; # goes with BBR
      "net.ipv4.tcp_fastopen" = 3; # saves a round-trip
      "net.ipv4.tcp_slow_start_after_idle" = 0; # don't reset cwnd after idle
      "net.ipv4.tcp_mtu_probing" = 1; # reduces fragmentation
      "net.ipv4.tcp_notsent_lowat" = 16384; # reduce latency

      "vm.dirty_writeback_centisecs" = 6000; # batch disk writes

    # zram is enabled on a lot of hosts, this optimizes it
    } // lib.optionalAttrs config.zramSwap.enable {
      "vm.page-cluster" = 0;
      "vm.watermark_boost_factor" = 0;
      "vm.watermark_scale_factor" = 125;
    };
  };

  networking = {
    dhcpcd.enable = false;
    useNetworkd = lib.mkDefault true; # newer
    wireless.iwd = {
      enable = lib.mkDefault true;
      settings = {
        IPv6.Enabled = true;
        Settings.AutoConnect = true;
        General.EnableNetworkConfiguration = false; # networkd handles DHCP now
        Network.NameResolvingService = "systemd";
        Scan.InitialPeriodicScanInterval = 10;
        Scan.MaximumPeriodicScanInterval = 30;
      };
    };
  };

  systemd.network = {
    enable = lib.mkDefault true;
    networks."20-default" = {
      matchConfig.Type = "ether wlan";
      networkConfig = {
        DHCP = "yes";
        IPv6AcceptRA = true;
        IgnoreCarrierLoss = "5s"; # tolerate short wifi drops
      };
      dhcpV4Config.UseMTU = true; # avoid fragmentation
    };
    wait-online = {
      timeout = 10; # Dont prolong boot for too long
      extraArgs = [ "--operational-state=routable" ];
    };
  };

  programs = {
    git = {
      enable = true;
      package = pkgs.gitMinimal;
      config = {
        init.defaultBranch = "main";
        color.ui = true;
        core.editor = "hx";
        credential.helper = "store --file=/home/dog/.local/share/git/credentials";
        github.user = "DerpyDoggo11"; # Github
        user.name = "DerpyDoggo11"; # Git
        push.autoSetupRemote = true;
      };
    };
    command-not-found.enable = false;
    nano.enable = false; # use Helix
  };
  security.sudo.extraConfig = "Defaults lecture=never"; # lectures are on by default

  time.timeZone = "America/Los_Angeles"; # lang also set to en_US
  zramSwap = {
    enable = lib.mkDefault true;
    memoryPercent = lib.mkDefault 100;
  };

  nixpkgs.config.allowUnfree = true;
  nix = {
    channel.enable = false; # we only use flakes

    settings = {
      experimental-features = "nix-command flakes";
      auto-optimise-store = true;
      warn-dirty = false;
      download-buffer-size = 268435456; # 256 MiB
      trusted-users = [ "dog" ]; # for remote deployments

      # Binary caches
      extra-substituters = [
        "https://attic.xuyh0120.win/lantian" # cachyos kernel
        "https://helix.cachix.org" # helix
      ];
      extra-trusted-public-keys = [
        "lantian:EeAUQ+W+6r7EtwnmYjeVwx5kOGEBpjlBfPlzGlTNvHc="
        "helix.cachix.org-1:ejp9KQpR1FBI2onstMQ34yogDm4OgU2ru6lIwPvuCVs="
      ];
    };
  };

  services = {
    openssh.settings = {
      PasswordAuthentication = false; # key auth only
      KbdInteractiveAuthentication = false;
    };

    journald.extraConfig = "SystemMaxUse=20M";

    avahi = {
      enable = true;
      nssmdns4 = true; # .local resolution
      nssmdns6 = true;
      openFirewall = true; # UDP5353
      publish = { # announce our own hostname (off by default!) so <host>.local resolves
        enable = true;
        addresses = true;
      };
    };
    resolved = {
      enable = true;
      settings.Resolve = {
        MulticastDNS = "no"; # avahi owns mDNS; resolved does unicast DNS only
        DNS = "1.1.1.1#cloudflare-dns.com 1.0.0.1#cloudflare-dns.com";
        DNSOverTLS = "opportunistic";
        Domains = "~."; # override DHCP-provided DNS (ISP)
      };
    };
  };
  fileSystems."/".options = [ "noatime" ];
  documentation.enable = false;
  environment.defaultPackages = lib.mkForce [];

  system.stateVersion = lib.mkDefault "24.05";
}
