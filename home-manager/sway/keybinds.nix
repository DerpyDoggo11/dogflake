{ pkgs, config, ... }: let
  mod = "Mod4";
  currentWorkspace = "$(swaymsg -p -t get_workspaces | grep focused | grep -oE '[0-9]+')";
  workspace = direction: "exec sh -c 'current=${currentWorkspace}; target=$((current ${direction} 1)); [ $target -lt 1 ] && target=1; [ $target -gt 9 ] && target=9; [ $target -eq $current ] || swaymsg workspace number $target'";
  moveItemToWorkspace = direction: "exec sh -c 'current=${currentWorkspace}; target=$((current ${direction} 1)); [ $target -lt 1 ] && target=1; [ $target -gt 9 ] && target=9; [ $target -ne $current ] && swaymsg move container to workspace number $target && swaymsg workspace number $target'";
  # True iff the focused workspace has apps other than foot/nemo
  shouldFloat = pkgs.writeShellScript "should-float" ''
    ws=$(swaymsg -t get_workspaces | ${pkgs.jq}/bin/jq -r '.[] | select(.focused) | .name')
    others=$(swaymsg -t get_tree | ${pkgs.jq}/bin/jq -r --arg ws "$ws" '
      ([.. | objects | select(.type? == "workspace" and .name? == $ws)][0] // {}) |
      [.. | objects | select(
        (.type? == "con" or .type? == "floating_con") and
        (.nodes? // [] | length) == 0 and
        (((.app_id? // "") | startswith("foot")) | not) and
        ((.app_id? // "") != "nemo")
      )] | length
    ')
    [ "''${others:-0}" -gt 0 ]
  '';
  openFoot = pkgs.writeShellScript "open-foot" ''
    if ${shouldFloat}; then
      exec footclient --app-id foot-float
    else
      exec footclient
    fi
  '';
  # todo is there a better way to do this? nemo has no app_id
  openNemo = pkgs.writeShellScriptBin "nemo" ''
    if ${shouldFloat}; then
      exec ${pkgs.nemo-with-extensions}/bin/nemo "$@"
    fi
    (
      ${pkgs.sway}/bin/swaymsg -t subscribe -m '["window"]' 2>/dev/null \
        | ${pkgs.jq}/bin/jq -c --unbuffered 'select(.change == "new" and (.container.app_id // "") == "nemo") | .container.id' \
        | head -n 1 \
        | while IFS= read -r con_id; do
            ${pkgs.sway}/bin/swaymsg "[con_id=$con_id] floating disable" >/dev/null
          done
    ) &
    watcher=$!
    ( sleep 2; kill $watcher 2>/dev/null ) &
    ${pkgs.nemo-with-extensions}/bin/nemo "$@"
    wait $watcher 2>/dev/null
  '';
  toggleTheme = pkgs.writeShellScript "toggle-theme" ''
    gs=${pkgs.glib}/bin/gsettings
    pk=${pkgs.procps}/bin/pkill
    themes=${config.gtk.theme.package}/share/themes
    gtk4css=$HOME/.config/gtk-4.0/gtk.css
    if [ "$($gs get org.gnome.desktop.interface color-scheme)" = "'prefer-dark'" ]; then
      $gs set org.gnome.desktop.interface color-scheme 'prefer-light'
      $gs set org.gnome.desktop.interface gtk-theme 'Graphite-Light-nord'
      ln -sfn "$themes/Graphite-Light-nord/gtk-4.0/gtk.css" "$gtk4css"
      $pk -USR2 -f "foot --server" || true # SIGUSR2 = [colors-light]
    else
      $gs set org.gnome.desktop.interface color-scheme 'prefer-dark'
      $gs set org.gnome.desktop.interface gtk-theme 'Graphite-Dark-nord'
      ln -sfn "$themes/Graphite-Dark-nord/gtk-4.0/gtk.css" "$gtk4css"
      $pk -USR1 -f "foot --server" || true # SIGUSR1 = [colors-dark]
    fi
  '';
in {
  home.packages = [ openNemo ]; # Shadow system nemo with wrapper

  wayland.windowManager.sway = {
    config.keybindings = {
      # Volume
      "XF86AudioRaiseVolume" = "exec wpctl set-volume @DEFAULT_SINK@ .05+";
      "XF86AudioLowerVolume" = "exec wpctl set-volume @DEFAULT_SINK@ .05-";
      "XF86AudioMute" = "exec wpctl set-mute @DEFAULT_SINK@ toggle";

      # Brightness
      "XF86MonBrightnessUp" = "exec brightnessctl set +15%";
      "XF86MonBrightnessDown" = "exec brightnessctl set 15%-";

      # Apps
      "${mod}+E" = "exec busctl --user call com.amazinaxel.lightbrowse /com/amazinaxel/lightbrowse org.gtk.Application Activate 'a{sv}' 0"; # lightbrowse
      "${mod}+return" = "exec ${openFoot}";

      # Ags
      "${mod}+space" = "exec ags toggle launcher";
      "${mod}+P" = "exec ags toggle pass";
      "${mod}+period" = "exec ags toggle emojiPicker";
      "${mod}+X" = "exec ags request closeAsideStatusMenuWidget";
      "control+${mod}+P" = "exec ags toggle passSave";
      "control+${mod}+z" = "exec ags request toggleQuicksettings";
      "control+${mod}+v" = "exec ags request toggleCalendar";
      "control+${mod}+x" = "exec ags request toggleBluetooth";
      "control+${mod}+c" = "exec ags request toggleWifi";

      "control+${mod}+Q" = "exec ags request sideviewPlan";
      "control+${mod}+W" = "exec ags request sideviewClaude";
      "control+${mod}+E" = "exec ags request sideviewCustom";
      "control+${mod}+R" = "exec ags request closeSideview"; # destroy
      "${mod}+A" = "exec ags request toggleSideviewFocus";
      "control+${mod}+Tab" = "exec ags request hideSideview"; # just hides
      "${mod}+G" = "exec ags request toggleSideviewSize";

      "${mod}+C" = "exec ags request hideNotif"; # Closes last notification
      "${mod}+N" = "exec ags request invokeOldestNotif"; # Activates first action of the oldest notification
      "${mod}+S" = "exec ags request toggleStreamingMode";
      "${mod}+O" = "exec ${toggleTheme}";
      "${mod}+T" = "exec ags request toggleFocus";
      "${mod}+Z" = "exec ags request toggleInfoArea";

      "${mod}+V" = "exec ags toggle clipboard";
      # Enter = select entry, C = copy 2nd entry, E = edit image with swappy, W = wipe clipboard

      "${mod}+R" = "exec ags request record";
      # R = toggle mic, Q = toggle quality, C = clip last 30s, Space = record

      "XF86PowerOff" = "exec ags toggle powermenu";
      "${mod}+shift+S" = "exec ags toggle powermenu";
      # S = sleep, Q = shutdown, L = lock, R = restart

      # Mpris
      "control+${mod}+S" = "exec playerctl play-pause"; # toggle play/pause
      "control+${mod}+D" = "exec playerctl next"; # toggle play/pause
      "control+${mod}+A" = "exec playerctl previous"; # toggle play/pause

      # blue light filter toggle
      "${mod}+b" = "exec ags request toggleFilter";

      # Ags MPD controls
      "${mod}+control+period" = "exec ags request 'media next'";
      "${mod}+control+comma" = "exec ags request 'media prev'";
      "${mod}+greater" = "exec ags request 'media nextPlaylist'";
      "${mod}+less" = "exec ags request 'media prevPlaylist'";
      "${mod}+slash" = "exec ags request 'media toggle'";

      "Print" = "exec screenshot"; # Screenshot

      # Move focus in a workspace
      "${mod}+left" = "focus left";
      "${mod}+right" = "focus right";
      "${mod}+up" = "focus up";
      "${mod}+down" = "focus down";
      "${mod}+h" = "focus left";
      "${mod}+l" = "focus right";
      "${mod}+k" = "focus up";
      "${mod}+j" = "focus down";

      # Move window in a workspace
      "${mod}+shift+left" = "move left";
      "${mod}+shift+right" = "move right";
      "${mod}+shift+up" = "move up";
      "${mod}+shift+down" = "move down";
      "${mod}+shift+h" = "move left";
      "${mod}+shift+l" = "move right";
      "${mod}+shift+k" = "move up";
      "${mod}+shift+j" = "move down";

      # Move through workspaces
      "control+${mod}+right" = workspace "+";
      "control+${mod}+left" = workspace "-";
      "control+${mod}+l" = workspace "+";
      "control+${mod}+h" = workspace "-";
      "control+${mod}+shift+right" = moveItemToWorkspace "+";
      "control+${mod}+shift+left" = moveItemToWorkspace "-";
      "control+${mod}+shift+l" = moveItemToWorkspace "+";
      "control+${mod}+shift+h" = moveItemToWorkspace "-";

      "${mod}+F" = "floating toggle";
      "F11" = "fullscreen toggle";
      "${mod}+Q" = "kill";
      "${mod}+tab" = "workspace back_and_forth";
    };
    # Scroll through workspaces
    extraConfig = ''
      bindsym --whole-window Mod4+button5 ${workspace "+"}
      bindsym --whole-window Mod4+button4 ${workspace "-"}
      bindsym --whole-window Control+Mod4+button5 ${moveItemToWorkspace "+"}
      bindsym --whole-window Control+Mod4+button4 ${moveItemToWorkspace "-"}
    '';
  };
}
